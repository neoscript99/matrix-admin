buildscript {
    repositories {
        mavenLocal()
        maven { url 'https://maven.aliyun.com/repository/public/' }
        mavenCentral()
        jcenter()
        maven { url "https://repo.grails.org/grails/core" }
    }
}

plugins {
    //plugins id 参数不能加gradle.properties变量
    id 'org.springframework.boot' version '2.2.1.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'groovy'
    id 'java-library'

    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.4"
}

group = 'com.feathermind.matrix'
version = '1.4'
sourceCompatibility = '1.8'

repositories {
    mavenLocal()
    maven { url 'https://maven.aliyun.com/repository/public/' }
    mavenCentral()
    jcenter()
    maven { url "https://repo.grails.org/grails/core" }
    maven { url "https://repo.gradle.org/gradle/libs-releases-local/" }
}

dependencies {
    api "org.grails:gorm-hibernate5-spring-boot:$gormVersion"
    implementation "org.hibernate:hibernate-core"
    implementation "org.hibernate:hibernate-ehcache"
    implementation "com.h2database:h2:1.4.200"

    implementation "org.apache.tomcat:tomcat-jdbc:8.5.0"
    implementation "org.apache.tomcat.embed:tomcat-embed-logging-log4j:8.5.0"
    api 'org.springframework.boot:spring-boot-starter-logging'
    api 'org.springframework.boot:spring-boot-starter-web'
    api 'org.springframework.boot:spring-boot-starter-security'

    //devtools存在Domain Class加载多次问题
    //https://github.com/grails/grails-data-mapping/issues/970
    //developmentOnly 'org.springframework.boot:spring-boot-devtools'


    implementation 'net.unicon.cas:cas-client-autoconfig-support:2.2.0-GA'
    api 'cn.hutool:hutool-core:5.0.3'
    api 'cn.hutool:hutool-crypto:5.0.3'
    implementation 'org.apache.poi:poi-ooxml:3.16'
    implementation 'com.baomidou:kaptcha-spring-boot-starter:1.1.0'
    implementation 'com.auth0:java-jwt:3.8.2'

    def gradleVersion = '5.6.4'
    testImplementation group: 'org.gradle', name: 'gradle-core', version: gradleVersion
    testImplementation group: 'org.gradle', name: 'gradle-core-api', version: gradleVersion
    testImplementation group: 'org.gradle', name: 'gradle-tooling-api', version: gradleVersion
    testImplementation group: 'org.gradle', name: 'gradle-plugins', version: gradleVersion
    testImplementation group: 'org.gradle', name: 'gradle-base-services', version: gradleVersion
    testImplementation group: 'org.gradle', name: 'gradle-base-services-groovy', version: gradleVersion

    testImplementation("org.spockframework:spock-spring:$spockVersion")
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

bootRun {
    jvmArgs('-Dspring.output.ansi.enabled=always')
    systemProperty 'spring.profiles.active', 'dev'
    systemProperty 'init', 'true'
    systemProperties System.properties
}

task sourceJar(type: Jar) {
    from(sourceSets.main.allSource, sourceSets.test.allSource)
}
jar {
    enabled = true
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
            groupId project.group
            artifactId project.name
            version project.version
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = project.name
        licenses = ['Apache-2.0']
        issueTrackerUrl = 'https://github.com/neoscript99/matrix-admin/issues'
        websiteUrl = 'https://github.com/neoscript99/matrix-admin.git'
        vcsUrl = 'https://github.com/neoscript99/matrix-admin'
        publicDownloadNumbers = true

        version {
            name = project.version
            desc = "$project.name $project.version"
            released = new Date()
            vcsTag = "v$project.version"
        }
    }
}
